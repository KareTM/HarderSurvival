plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "8.3.9"
}

group = 'kare'
version = '1.2.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url 'https://nexus.frengor.com/repository/public/' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    implementation 'com.jeff-media:custom-block-data:2.2.4'
    implementation 'com.frengor:ultimateadvancementapi-shadeable:2.7.1:mojang-mapped'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.11.5'
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.10")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'com.jeff_media.customblockdata', 'kare.kareHardSurvival.customblockdata'
    relocate 'com.frengor.ultimateAdvancementAPI', 'kare.kareHardSurvival.ultimateAdvancementAPI'
    relocate 'com.github.stefvanschie.inventoryframework', 'kare.kareHardSurvival.inventoryframework'
}

build.dependsOn += shadowJar